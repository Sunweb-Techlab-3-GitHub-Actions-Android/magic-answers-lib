plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'org.jetbrains.dokka' version '0.10.0'
    id 'io.gitlab.arturbosch.detekt' version '1.18.1'
}

group = 'com.woowla.magicanswerslib'
version = "0.0.5"

android {
    compileSdk 30

    defaultConfig {
        minSdk 22
        targetSdk 30
        versionCode 1
        versionName "1.1.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'

    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.18.1"

    testImplementation 'junit:junit:4.+'

    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

detekt {
    toolVersion = "1.18.1"
    config = files("../detekt.yml")
    buildUponDefaultConfig = true
    ignoreFailures = false
}

dokka {
    outputFormat = 'html' // use 'javadoc' to get standard java docs
    outputDirectory = "$buildDir/javadoc"

    configuration {
        includeNonPublic = false
        skipEmptyPackages = true
        skipDeprecated = false
        reportUndocumented = true
        jdkVersion = 8
    }
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.source
}

task androidDokka(type: Jar) {
    classifier = 'javadoc'
    from tasks.dokka
}

afterEvaluate {
    publishing {
        repositories {
            publications {
                // Creates a Maven publication called "release".
                release(MavenPublication) {
                    // Applies the component for the release build variant.
                    from components.release
                    artifact androidSourcesJar
                    artifact androidDokka
                }
            }
            // Uncomment this to publish in project build directory
            //maven { url = "$buildDir/repository" }
            maven {
                name "GitHubPackages"
                url "https://maven.pkg.github.com/Sunweb-Techlab-3-GitHub-Actions-Android"
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}
